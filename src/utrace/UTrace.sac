module UTrace;

 /******************************************************************************
 *
 * Depends on Array, ArrayFormat, ArrayIO, ScalarIO, String, TermFile, Indent.
 *
 ******************************************************************************/

use Array: { sel };
use ArrayFormat: { format };
use String: { string, strlen, to_string };
use TermFile: { printf };
use Indent: all;

export all except { offset, whitespace, printSeparator, printHeader };

#define BUILT_IN(fun)                                                          \
fun(int)                                                                       \
fun(float)                                                                     \
fun(double)                                                                    \
fun(bool)                                                                      \
fun(char)

objdef Indent offset = newIndent(0);

char[n] whitespace(int n)
{
    return { [i] -> ' ' | [i] < [n] };
}

void printSeparator(string pattern)
{
    doIndent(pattern);
    printf("----------------------------------------------\n");
}

void printHeader(string modName, int line)
{
    doIndent("   ");
    printf("%-15s: line %3d: ", modName, line);
    setIndent(offset, 27);
}

/******************************************************************************/

#define SHOW(typ)                                                              \
void indentedShow(typ[d>1:shp] arr)                                            \
{                                                                              \
    cshape = to_string(format(shp));                                           \
    printf("reshape([ %s ] , [\n", cshape);                                    \
                                                                               \
    spaces = whitespace(_add_SxS_(getIndent(),                                 \
                        _add_SxS_(getIndent(offset),                           \
                        _add_SxS_(strlen(cshape), 18))));                      \
                                                                               \
    carr = format(arr);                                                        \
    ext_carr = with {                                                          \
        (. <= [i] <= . ) : _cat_VxV_(spaces, carr[[i]]);                       \
    } : genarray([_sel_VxA_([0], _shape_A_(carr))],                            \
                 _cat_VxV_(spaces,                                             \
                           whitespace(_sel_VxA_([1], _shape_A_(carr)))));      \
                                                                               \
    ArrayIO::show(ext_carr);                                                   \
    incIndent(offset, _add_SxS_(strlen(cshape), 16));                          \
    doIndent("   ");                                                           \
    doIndent(offset, " ");                                                     \
    printf("])\n");                                                            \
    decIndent(offset, _add_SxS_(strlen(cshape), 16));                          \
}                                                                              \
                                                                               \
void indentedShow(typ[.,.] arr)                                                \
{                                                                              \
    spaces = whitespace(_add_SxS_(getIndent(), getIndent(offset)));            \
                                                                               \
    carr = format(arr);                                                        \
    ext_carr = { [i] -> _eq_SxS_(i, 0)                                         \
                            ? _cat_VxV_(carr[[i]], spaces)                     \
                            : _cat_VxV_(spaces, carr[[i]]) };                  \
    ArrayIO::show(ext_carr);                                                   \
}                                                                              \
                                                                               \
void indentedShow(typ[.] arr)                                                  \
{                                                                              \
    ArrayIO::show(arr);                                                        \
}                                                                              \
                                                                               \
void indentedShow(typ arr)                                                     \
{                                                                              \
    ScalarIO::show(arr);                                                       \
}

BUILT_IN(SHOW)

/******************************************************************************/

void PrintFunEnter(string modName, int line, string funName)
{
    incIndent();
    printSeparator(">  ");
    printHeader(modName, line);
    printf("%s (\n",  funName);
    incIndent(offset, _add_SxS_(strlen(funName), 3));
}

void PrintFunLeave(string modName, int line, string funName)
{
    printSeparator("<  ");
    decIndent();
    decIndent(offset, _add_SxS_(strlen(funName), 2));
}

void PrintArgsDone(string modName, int line, string funName)
{
    doIndent("   ");
    doIndent(offset, " ");
    printf(");\n");
}

#define PRINT_ARG(typ)                                                         \
void PrintArg(string modName, int line, string var, typ[*] arr)                \
{                                                                              \
    doIndent("   ");                                                           \
    doIndent(offset, " ");                                                     \
    printf(" %s = ", var);                                                     \
    incIndent(offset, _add_SxS_(strlen(var), 4));                              \
    indentedShow(arr);                                                         \
    decIndent(offset, _add_SxS_(strlen(var), 4));                              \
}

BUILT_IN(PRINT_ARG)

#define PRINT_ASSIGN(typ)                                                      \
void PrintAssign(string modName, int line, string var, typ[*] arr)             \
{                                                                              \
    printHeader(modName, line);                                                \
    printf("%s = ", var);                                                      \
    incIndent(offset, _add_SxS_(strlen(var), 3));                              \
    indentedShow(arr);                                                         \
    decIndent(offset, _add_SxS_(strlen(var), 3));                              \
}

BUILT_IN(PRINT_ASSIGN)

#define PRINT_RETURN(typ)                                                      \
void PrintReturn(string modName, int line, typ[*] arr)                         \
{                                                                              \
    printHeader(modName, line);                                                \
    printf("returning ");                                                      \
    incIndent(offset, 10);                                                     \
    indentedShow(arr);                                                         \
    decIndent(offset, 10);                                                     \
}

BUILT_IN(PRINT_RETURN)
