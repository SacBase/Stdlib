module StringArray;

use String : {string};
use Array : {*,all,-,abs,<,>=,!=,+,zero,drop,any,prod,iota,reshape,sum,transpose};
import Array: { ==, ++ };

export all except { stringArrayCreator, modarray_AxVxS, sel_VxA, getIndices };

/******************************************************************************
 *
 * You can create a new stringArray using:
 *
 *   genarray(shp, to_stringArray("default string"))
 *
 * Modarray and sel work as usual and return stringarrays.
 * Use to_string to get a string back out of a scalar stringArray.
 *
 ******************************************************************************/

external typedef stringArray;
    #pragma linkobj "src/StringArray/free.o"
    #pragma linkobj "src/StringArray/copy.o"
    #pragma linkobj "src/StringArray/alloc.o"
    #pragma freefun "SAC_StringArray_free"
    #pragma copyfun "SAC_StringArray_copy"

external int dim(stringArray s);
    #pragma linkname "SAC_StringArray_dim"
    #pragma linkobj "src/StringArray/dim.o"
    #pragma refcounting [0,1]
    #pragma linksign [1,2]

external int[.] shape(stringArray s);
    #pragma linkname "SAC_StringArray_shape"
    #pragma linkobj "src/StringArray/shape.o"
    #pragma refcounting [0,1]
    #pragma linksign [1,2]

external stringArray stringArrayCreator(int[.] shp, string s);
    #pragma linkname "SAC_StringArray_genarray"
    #pragma linkobj "src/StringArray/genarray.o"
    #pragma refcounting [0,1,2]
    #pragma linksign [1,2,3]

external stringArray modarray_AxVxS(stringArray labs, int[.] idx, string  str);
    #pragma linkname "SAC_StringArray_modarray"
    #pragma linkobj "src/StringArray/modarray.o"
    #pragma linkobj "src/StringArray/index2offset.o"
    #pragma refcounting [0,1,2,3]
    #pragma linksign [1,2,3,4]

external string sel_VxA(int[.] idx, stringArray labs);
    #pragma linkname "SAC_StringArray_sel"
    #pragma linkobj "src/StringArray/sel.o"
    #pragma linkobj "src/StringArray/index2offset.o"
    #pragma refcounting [0,1,2]
    #pragma linksign [1,2,3]

stringArray modarray(stringArray arr, int[n] idx, stringArray val)
{
    if (any(idx < 0) || any(idx >= shape(arr))) {
        RuntimeError::error(1,
            "Index out of bounds in call to StringArray::modarray");
    }

    if (n == dim(arr)) {
        if (dim(val) != 0) {
            RuntimeError::error(1,
                "Attempt to use StringArray::modarray to replace scalar "
                "subarray with non-scalar");
        }

        res = modarray_AxVxS(arr, idx, sel_VxA([], val));
    }
    else if (n < dim(arr)) {
        if (any(shape(val) != drop([n], shape(arr)))) {
            RuntimeError::error(1,
                "Attempt to use StringArray::modarray to replace subarray "
                "with an arr of a different shape");
        }

        val_shp = drop([n], shape(arr));
        indices = getIndices(val_shp);
        res = arr;
        ub = Array::sel([0], Array::shape(indices));
        for (i = 0; i < ub; i += 1) {
            selection = Array::sel([i], indices);
            modarrayidx = idx ++ selection;
            modArrVal = sel_VxA(Array::sel([i], indices), val);
            res = modarray_AxVxS(res, modarrayidx, modArrVal);
        }
    }
    else {
        res = arr;
        RuntimeError::error(1,
            "Overlarge index vector passed to StringArray::modarray");
    }

    return res;
}

stringArray sel(int[n] idx, stringArray arr)
{
    if (any(idx < 0) || any(idx >= shape(arr))) {
        RuntimeError::error(1,
            "Index out of bounds in call to StringArray::sel");
    }

    if (n == dim(arr)) {
        res = to_stringArray(sel_VxA(idx, arr));
    }
    else if (n < dim(arr)) {
        res_shp = drop([n], shape(arr));
        if (Array::sel([0], res_shp) == 0) {
            RuntimeError::error(1, "res_shape is [0]");
        }

        res = genarray(res_shp, to_stringArray(""));

        indices = getIndices(shape(res));
        for (i=0; i < Array::sel([0], Array::shape(indices)); i += 1) {
            res = modarray(res, Array::sel([i], indices),
                        sel(idx ++ Array::sel([i], indices), arr));
        }
    }
    else {
        res = arr;
        RuntimeError::error(1,
            "Attempt to select into a stringArray with an index vector "
            "longer than the stringArray's dimensionality");
    }

    return res;
}

stringArray sel(int idx, stringArray arr)
{
    return sel([idx], arr);
}

string to_string(stringArray a)
{
    if (dim(a) != 0) {
        RuntimeError::error(1,
            "StringArray::to_string passed non-scalar stringArray");
    }

    return sel_VxA([], a);
}

stringArray to_stringArray(string s)
{
    return stringArrayCreator([], s);
}

stringArray genarray(int[.] shp, stringArray s)
{
    new_shp = shp ++ shape(s);
    res = stringArrayCreator(new_shp, "");

    indices = getIndices(shp);
    for (i = 0; i < Array::sel([0], Array::shape(indices)); i += 1) {
        res = modarray(res, Array::sel([i], indices), s);
    }

    return res;
}

inline
bool[*] ==(stringArray a, stringArray b)
{
    if (dim(a) == 0) {
        if (dim(b) == 0) {
            res = to_string(a) String::== to_string(b);
        } else {
            res = { iv -> to_string(a) String::== sel_VxA(iv, b)
                  | iv < shape(b) };
        }
    } else {
        if (dim(b) == 0) {
            res = { iv -> sel_VxA(iv, a) String::== to_string(b)
                  | iv < shape(a) };
        } else if (all(shape(a) == shape(b))) {
            res = { iv -> sel_VxA(iv, a) String::== sel_VxA(iv, b)
                  | iv < shape(a) };
        } else {
            RuntimeError::error(1,
                "StringArray::== applied to arrays of different shape!");
            res = false;
        }
    }

    return res;
}

int[o,d] getIndices(int[d] shp)
{
    o = prod(shp);
    return reshape([o,d], iota(shp));
}

stringArray ++(stringArray m1, stringArray m2)
{
    if (dim(m1) == 0) {
        RuntimeError::error(1,
            "Cannot concat stringArrays since argument 1 is a scalar");
    }
    if (dim(m2) == 0) {
        RuntimeError::error(1,
            "Cannot concat stringArrays since argument 2 is a scalar");
    }
    if (dim(m1) != dim(m2)) {
        RuntimeError::error(1,
            "Cannot concat stringArrays with differing dimensions");
    }

    m1InnerShape = drop([1], shape(m1));
    m2InnerShape = drop([1], shape(m2));
    if (any(m1InnerShape != m2InnerShape)) {
        RuntimeError::error(1,
            "Attempt to concat non-matching shapes when trying to "
            "concat two stringArrays");
    }

    m1Len = Array::sel([0], shape(m1));
    m2Len = Array::sel([0], shape(m2));
    resShape = [ m1Len + m2Len] ++ m1InnerShape;
    res = genarray(resShape, to_stringArray(""));

    for (i = 0; i < m1Len; i += 1) {
        res = modarray(res, [i], m1[i]);
    }
    for (j = 0; j < m2Len; j += 1) {
        res = modarray(res, [i + j], m2[i]);
    }

    return res;
}

/*
 * Returns true if needle exists in haystack
 */
bool in(string needle, stringArray haystack)
{
    return any({ iv -> to_string(haystack[iv]) String::== needle
               | iv < shape(haystack) });
}

/*
 * Returns the index into haystack for the string needle.
 * N.b. before calling this function call in to ensure that the needle you are
 * searching for really is in the haystack!
 */
int[n] indexOf(string needle, stringArray haystack)
{
    indices = getIndices(shape(haystack));

    res = { [i] -> to_string(haystack[Array::sel([i], indices)]) String::== needle
                    ? Array::sel([i], indices)
                    : 0 * Array::sel([i], indices)
          | [i] < [Array::sel([0], Array::shape(indices))] };

    res = transpose(res);

    res = { [i] -> sum(Array::sel([i], res))
          | [i] < [Array::sel([0], Array::shape(res))] };

    return res;
}
