module ComplexArrayBasics;

/******************************************************************************
 *
 * Depends on ComplexBasics and ArrayBasics.
 *
 ******************************************************************************/

use ComplexBasics: { complex };

export all;

/******************************************************************************
 *
 * @fn int dim(complex[*] arr)
 *
 * @brief Yields an array's dimensionality (rank).
 *
 * @note The definition of `dim` and `shape` for user-defined types may not use
 * type patterns, as type patterns rely on the existence of these functions
 * for user-defined types since the primitives `_dim_A_` and `_shape_A_`
 * are not defined for user-defined types.
 *
 ******************************************************************************/
inline int dim(complex[*] arr)
{
    return _sub_SxS_(_dim_A_((double[+])arr), 1);
}

/******************************************************************************
 *
 * @fn int[.] shape(complex[*] arr)
 *
 * @brief Yields an array's shape vector.
 *
 * @note The definition of `dim` and `shape` for user-defined types may not use
 * type patterns, as type patterns rely on the existence of these functions
 * for user-defined types since the primitives `_dim_A_` and `_shape_A_`
 * are not defined for user-defined types.
 *
 ******************************************************************************/
inline int[.] shape(complex[*] arr)
{
    shp = _shape_A_((double[+])arr);
    shp_len = _sel_VxA_([0], _shape_A_(shp));
    return _take_SxV_(_sub_SxS_(shp_len, 1), shp);
}

/******************************************************************************
 *
 * @fn complex[i:ishp] sel(int[o] idx, complex[o:oshp,i:ishp] arr)
 *
 * @brief Generalizes selections for scalar indices.
 *
 ******************************************************************************/
inline complex[i:ishp] sel(int[o] idx, complex[o:oshp,i:ishp] arr)
    | _all_V_(_le_SxV_(0, idx)), _all_V_(_lt_VxV_(idx, oshp))
{
    return (complex[*])ArrayBasics::sel(idx, (double[+])arr);
}

inline complex[d:shp] sel(int idx, complex[n,d:shp] arr)
    | _le_SxS_(0, idx), _lt_SxS_(idx, n)
{
    return arr[[idx]];
}

/******************************************************************************
 *
 * @fn complex[d:shp] reshape(int[d] shp, complex[o:oshp] arr)
 *
 * @brief Creates a new array with identical data vector but new shape, provided
 * the length of the data vector matches the product of the new shape vector.
 *
 ******************************************************************************/
inline complex[d:shp] reshape(int[d] shp, complex[o:oshp] arr)
    | _all_V_(_le_SxV_(0, shp))
{
    return (complex[*])_reshape_VxA_(_cat_VxV_(shp, [2]), (double[*])arr);
}

/******************************************************************************
 *
 * @fn complex[n:shp,m:vshp] genarray(int[n] shp, complex[m:vshp] val)
 *
 * @brief Generates a new array from the given shape and default value.
 *
 ******************************************************************************/
inline complex[n:shp,m:vshp] genarray(int[n] shp, complex[m:vshp] val)
{
    return { iv -> val | iv < shp };
}

/******************************************************************************
 *
 * @fn complex[o:oshp,i:ishp] modarray(complex[o:oshp,i:ishp] arr,
 *                                     int[o] idx, complex[i:ishp] val)
 *
 * @brief Modifies the element of array at position idx.
 *
 ******************************************************************************/
inline complex[o:oshp,i:ishp] modarray(complex[o:oshp,i:ishp] arr,
                                       int[o] idx, complex[i:ishp] val)
    | _all_V_(_le_SxV_(0, idx)), _all_V_(_lt_VxV_(idx, oshp))
{
    return with {
        (idx <= iv <= idx) : val;
    } : modarray(arr);
}

/******************************************************************************
 *
 * @fn complex[n,d:shp] modarray(complex[n,d:shp] arr, int idx,
 *                               complex[d:shp] val)
 *
 * @brief Modifies the element of array at position [idx].
 *
 ******************************************************************************/
inline complex[n,d:shp] modarray(complex[n,d:shp] arr, int idx,
                                 complex[d:shp] val)
    | _le_SxS_(0, idx), _lt_SxS_(idx, n)
{
    return modarray(arr, [idx], val);
}

/******************************************************************************
 *
 * @fn double[d:shp] norm(complex[d:shp] arr)
 *
 * @brief Yields an array of norms.
 *
 ******************************************************************************/
inline double[d:shp] norm(complex[d:shp] arr)
{
    return { iv -> ComplexBasics::norm(arr[iv]) | iv < shp };
}

/******************************************************************************
 *
 * @fn double[d:shp] normSq(complex[d:shp] arr)
 *
 * @brief Yields an array of normSq.
 *
 ******************************************************************************/
inline double[d:shp] normSq(complex[d:shp] arr)
{
    return { iv -> ComplexBasics::normSq(arr[iv]) | iv < shp };
}
