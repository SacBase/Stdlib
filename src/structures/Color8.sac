module Color8;

/******************************************************************************
 *
 * Depends on Array and Numerical.
 *
 ******************************************************************************/

use Numerical: { exp, log };

export all except { genSteps, weights2Clut };

typedef int[3] Color8;

/******************************************************************************
 *
 * Array properties.
 *
 * @note The definition of `dim` and `shape` for user-defined types may not use
 * type patterns, as type patterns rely on the existence of these functions
 * for user-defined types since the primitives `_dim_A_` and `_shape_A_`
 * are not defined for user-defined types.
 *
 ******************************************************************************/

inline int dim(Color8[d:shp] arr)
{
    return d;
}

inline int[d] shape(Color8[d:shp] arr)
{
    return shp;
}

inline Color8[i:ishp] sel(int[o] idx, Color8[o:oshp,i:ishp] arr)
    | _all_V_(_le_SxV_(0, idx)), _all_V_(_lt_VxV_(idx, oshp))
{
    return (Color8[*])Array::sel(idx, (int[*])arr);
}

inline Color8[d:shp] sel(int idx, Color8[n,d:shp] arr)
    | _le_SxS_(0, idx), _lt_SxS_(idx, n)
{
    return arr[[idx]];
}

inline
Color8[d:shp] reshape(int[d] shp, Color8[_:shp_a] arr)
{
    return (Color8[*])_reshape_VxA_(_cat_VxV_(shp, [3]), (int[*])arr);
}

/******************************************************************************
 *
 * Type conversions.
 *
 ******************************************************************************/

inline Color8 newColor(int[3] c)
{
    min_col = [0, 0, 0];
    max_col = [255, 255, 255];
    c = _max_VxV_(_min_VxV_(c, max_col), min_col);
    return (Color8)c;
}

inline Color8 newColor(int r, int g, int b)
{
    return newColor([r,g,b]);
}

inline int[3] toi(Color8 c)
{
    return (int[3])c;
}

inline int[d:shp,3] toi(Color8[d:shp] c)
{
    return (int[*])c;
}

inline double[3] tod(Color8 c)
{
    return Array::tod((int[3])c);
}

inline double[d:shp,3] tod(Color8[d:shp] c)
{
    return Array::tod((int[+])c);
}

inline int red(Color8 c)
{
    return _sel_VxA_([0], (int[3])c);
}

inline int green(Color8 c)
{
    return _sel_VxA_([1], (int[3])c);
}

inline int blue(Color8 c)
{
    return _sel_VxA_([2], (int[3])c);
}

inline int, int, int rgb(Color8 c)
{
    return (red(c), green(c), blue(c));
}

/******************************************************************************
 *
 * Color operations.
 *
 ******************************************************************************/

inline double[256] genSteps(double from, double to)
{
    return { [i] -> _add_SxS_(from,
                              _div_SxS_(_tod_S_(i),
                                        _mul_SxS_(256d, _sub_SxS_(to, from))))
           | [i] < [256] };
}

inline Color8 weights2Clut(double w, Color8 c1, Color8 c2)
{
    r1, g1, b1 = rgb(c1);
    r2, g2, b2 = rgb(c2);
    r = _toi_S_(_add_SxS_(_mul_SxS_(w, _tod_S_(r1)),
                          _sub_SxS_(1d, _mul_SxS_(w, _tod_S_(r2)))));
    g = _toi_S_(_add_SxS_(_mul_SxS_(w, _tod_S_(g1)),
                          _sub_SxS_(1d, _mul_SxS_(w, _tod_S_(g2)))));
    b = _toi_S_(_add_SxS_(_mul_SxS_(w, _tod_S_(b1)),
                          _sub_SxS_(1d, _mul_SxS_(w, _tod_S_(b2)))));
    return (Color8)[r,g,b];
}

inline Color8[256] weights2Clut(double[256] w, Color8 c1, Color8 c2)
{
    return { iv -> weights2Clut(_sel_VxA_(iv, w), c1, c2) | iv < [256] };
}

inline Color8[256] genLinearClut(Color8 c1, Color8 c2)
{
    weights = genSteps(0d, 1d);
    return weights2Clut(weights, c1, c2);
}

inline Color8[256] genLogarithmicClut(double from, double to, Color8 c1, Color8 c2)
{
    steps = genSteps(from, to);
    weights = _div_VxS_(_sub_VxS_(log(steps), log(from)),
                        _sub_SxS_(log(to), log(from)));
    return weights2Clut(weights, c1, c2);
}

inline Color8[256] genExponentialClut(double from, double to, Color8 c1, Color8 c2)
{
    steps = genSteps(from, to);
    weights = _div_VxS_(_sub_VxS_(exp(steps), exp(from)),
                        _sub_SxS_(exp(to), exp(from)));
    return weights2Clut(weights, c1, c2);
}

inline Color8[256] genAlternatingClut(Color8 c1, Color8 c2)
{
    return with {
        ( . <= [i] <= . step [2]) : c1;
    } : genarray([256], c2);
}

/*
 * h_in is the hue in degrees [0:360]
 * s_in is the saturation in % [0:100]
 * b_in is the brightness in % [0:100]
 */
inline Color8 Hsb2Rgb(int h_in, int s_in, int b_in)
{
    c = black();

    if (_eq_SxS_(b_in, 0)) {
        c = black();
    } else if (_eq_SxS_(b_in, 100) && _eq_SxS_(s_in, 0)) {
        c = white();
    } else if (_eq_SxS_(s_in, 0)) {
        c = newColor(b_in, b_in, b_in);
    } else {
        h = _div_SxS_(_tod_S_(h_in), 60d);
        s = _div_SxS_(_tod_S_(s_in), 100d);
        b = _div_SxS_(_tod_S_(b_in), 100d);

        i = _mod_SxS_(_toi_S_(h), 6);
        f = _sub_SxS_(h, _tod_S_(i));

        if (_eq_SxS_(_mod_SxS_(i, 2), 0)) {
            f = _sub_SxS_(1d, f);
        }

        m = _mul_SxS_(b, _sub_SxS_(1d, s));
        n = _mul_SxS_(b, _sub_SxS_(1d, _mul_SxS_(s, f)));

        b = _toi_S_(_mul_SxS_(b, 255d));
        m = _toi_S_(_mul_SxS_(m, 255d));
        n = _toi_S_(_mul_SxS_(n, 255d));

        if (_eq_SxS_(0, i)) {
            c = newColor(b, n, m);
        } else if (_eq_SxS_(1, i)) {
            c = newColor(n, b, m);
        } else if (_eq_SxS_(2, i)) {
            c = newColor(m, b, n);
        } else if (_eq_SxS_(3, i)) {
            c = newColor(m, n, b);
        } else if (_eq_SxS_(4, i)) {
            c = newColor(n, m, b);
        } else if (_eq_SxS_(5, i)) {
            c = newColor(b, m, n);
        }
    }

    return c;
}

inline Color8[d:shp] Hsb2Rgb(int[d:shp] h, int s, int b)
{
    return { iv -> Hsb2Rgb(_sel_VxA_(iv, h), s, b) | iv < shp };
}

inline Color8[d:shp] Hsb2Rgb(int[d:shp] h, int[d:shp] s, int[d:shp] b)
{
    return { iv -> Hsb2Rgb(_sel_VxA_(iv, h),
                           _sel_VxA_(iv, s),
                           _sel_VxA_(iv, b))
           | iv < shp };
}

/******************************************************************************
 *
 * Binary operations.
 *
 ******************************************************************************/

inline Color8 +(Color8 a, Color8 b)
{
    r1, g1, b1 = rgb(a);
    r2, g2, b2 = rgb(b);
    return newColor(_add_SxS_(r1, r2), _add_SxS_(g1, g2), _add_SxS_(b1, b2));
}

inline Color8[d:shp] +(Color8[d:shp] a, Color8[d:shp] b)
{
    return { iv -> a[iv] + b[iv] | iv < shp };
}

inline Color8 -(Color8 a, Color8 b)
{
    r1, g1, b1 = rgb(a);
    r2, g2, b2 = rgb(b);
    return newColor(_sub_SxS_(r1, r2), _sub_SxS_(g1, g2), _sub_SxS_(b1, b2));
}

inline Color8[d:shp] -(Color8[d:shp] a, Color8[d:shp] b)
{
    return { iv -> a[iv] - b[iv] | iv < shp };
}

inline Color8 *(Color8 c, double x)
{
    r, g, b = rgb(c);
    return newColor(
        _toi_S_(_mul_SxS_(_tod_S_(r), x)),
        _toi_S_(_mul_SxS_(_tod_S_(g), x)),
        _toi_S_(_mul_SxS_(_tod_S_(b), x))
    );
}

inline Color8[d:shp] *(Color8[d:shp] c, double x)
{
    return { iv -> c[iv] * x | iv < shp };
}

/******************************************************************************
 *
 * Color constants.
 *
 ******************************************************************************/

inline Color8 black()
{
    return (Color8)[0,0,0];
}

inline Color8 white()
{
    return (Color8)[255,255,255];
}

inline Color8 red()
{
    return (Color8)[255,0,0];
}

inline Color8 green()
{
    return (Color8)[0,255,0];
}

inline Color8 blue()
{
    return (Color8)[0,0,255];
}
